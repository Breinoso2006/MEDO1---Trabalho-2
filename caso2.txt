clear;
clc;
close;

//Variáveis com valores iniciais da planilha

varCi = 0;
varCw = 10;
varAlpha = 40e-6;
varComprimento = 300;

//Variáveis com valores iniciais, criadas para cálculo

nosEspaco = 61;
nosTempo = 100;
tempoTotal = 10000000000;
tolerancia = 1e-10;
tempoConvergencia = 50;

//Variaveis sem valores iniciais, criadas para cálculos

deltaX = varComprimento/nosEspaco;
deltaT = tempoTotal/nosTempo;
s = (varAlpha*deltaT)/(deltaX * deltaX);

//Vetores utilizados para cálculo

    //Vetor de espaço

    vetEspaco(1) = 0;
    for i = 2:nosEspaco
        vetEspaco(i) = vetEspaco(i-1) + deltaX;
    end

    //Geração dos vetores com valores de concetração

    for i=1:nosEspaco
        vetAntigo(i) = 0;
        vetNovo(i) = 0;
    end

    for i=2:nosEspaco-1
        vetAntigo(i) = varCi;
        vetNovo(i) = varCi;
    end

    vetAntigo(1) = varCw;
    vetNovo(1)= varCw;

//Resolução

t = 0;
while t < tempoTotal
    for i = 1:tempoConvergencia
        vetAntigo(nosEspaco) = vetAntigo(nosEspaco-1);
        vetNovo(nosEspaco) = vetNovo(nosEspaco-1); 
        erro = -1;
        for j = 2:nosEspaco-1
            aux = vetNovo(j);
            vetNovo(j) = (s*(vetNovo(j-1) + vetNovo(j+1))+vetAntigo(j))/(2*s+1);
            erro = max(erro,abs((vetNovo(j) - aux)/vetNovo(j)));
        end
        if (erro<tolerancia)
            break;
        end
    end
    vetAntigo = vetNovo;
    t = t + deltaT;
end

//Plot

plot2d(vetEspaco, vetNovo,2);
title("Gráfico Espaço X Concentração",'fontsize',3);
xlabel("Cm",'fontsize',3);
ylabel("C(mol/cm^3)",'fontsize',3);
xgrid();
